*** Settings ***
Library             Selenium2Library
Suite Setup         Open Contact Page
Suite Teardown      Close All Browsers

*** Variables ***
${URL}              http://localhost:3333
${SUCCESS_MAIL}     test@example.com
${ERROR_MAIL}       wingyminov@mail.com
${SUBJECT}          Test subject
${MESSAGE}          Test message

*** Test Cases ***
Pop Kung would Like To Send Feedback With Invalid Email
    Given He Fill Invalid Email With Valid Subject And Content
    When He Submit Form Back
    Then Feedback Form Should Not Be Delivered

Pop Kung Would Like To Send Feedback with Valid Email
    Given He Fill In All Valid Information In Feedback Form
    When He Submit Form Back
    Then Feedback Form Should Be Delivered To Whom It May Concern

*** Keywords ***
He Fill In All Valid Information In Feedback Form
    Fill In Form With Success Data Test
    Send Button Should Be Enabled

He Fill Invalid Email With Valid Subject And Content
    Fill In Form With Fail Data Test
    Send Button Should Be Enabled

He Submit Form Back
    Click Send Button

Feedback Form Should Be Delivered To Whom It May Concern
    Page Display Send Message Success
    Click OK Button
    Form Display With Blank Data

Feedback Form Should Not Be Delivered
    Page Display Send Message Fail
    Click Dismiss Button
    Form Display With Fill In Data

Open Contact Page
    Open Browser    url=${URL}    browser=gc
    Set Window Size    1280    800

Fill In Form With Success Data Test
    Input Text    email      ${SUCCESS_MAIL}
    Fill Form For Subject And Content

Fill In Form With Fail Data Test
    Input Text    email      ${ERROR_MAIL}
    Fill Form For Subject And Content

Fill Form For Subject And Content
    Input Text    subject    ${SUBJECT}
    Press Key     message    ${MESSAGE}

Send Button Should Be Enabled
    Element Should Be Enabled    send-button

Click Send Button
    Click Button    send-button

Page Display Send Message Success
    Element Should Be Visible    overlay
    Element Should Be Visible    success-message
    Element Should Be Enabled    success-button

Page Display Send Message Fail
    Element Should Be Visible    overlay
    Element Should Be Visible    error-message
    Element Should Be Enabled    error-button

Click OK Button
    Click Button    success-button

Click Dismiss Button
    Click Button    error-button

Form Display With Blank Data
    Textfield Value Should Be    subject    ${EMPTY}
    Textarea Value Should Be     message    ${EMPTY}

Form Display With Fill In Data
    Textfield Value Should Be    subject    ${SUBJECT}
    Textarea Value Should Be     message    ${MESSAGE}
